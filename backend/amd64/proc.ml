(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*             Xavier Leroy, projet Cristal, INRIA Rocquencourt           *)
(*                                                                        *)
(*   Copyright 2000 Institut National de Recherche en Informatique et     *)
(*     en Automatique.                                                    *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

[@@@ocaml.warning "+a-40-41-42"]

(* Description of the AMD64 processor *)

open! Int_replace_polymorphic_compare

open Misc
open Arch
open Cmm

let fp = Config.with_frame_pointers

(* Which ABI to use *)

let win64 = Arch.win64

(* Registers available for register allocation *)

(* Register map:
    rax         0
    rbx         1
    rdi         2
    rsi         3
    rdx         4
    rcx         5
    r8          6
    r9          7
    r12         8
    r13         9
    r10         10
    r11         11
    rbp         12
    r14         domain state pointer
    r15         allocation pointer

  xmm0 - xmm15  100 - 115 *)

(* Conventions:
     rax - r13: OCaml function arguments
     rax: OCaml and C function results
     xmm0 - xmm9: OCaml function arguments
     xmm0: OCaml and C function results
   Under Unix:
     rdi, rsi, rdx, rcx, r8, r9: C function arguments
     xmm0 - xmm7: C function arguments
     rbx, rbp, r12-r15 are preserved by C
     xmm registers are not preserved by C
   Under Win64:
     rcx, rdx, r8, r9: C function arguments
     xmm0 - xmm3: C function arguments
     rbx, rbp, rsi, rdi r12-r15 are preserved by C
     xmm6-xmm15 are preserved by C
   Note (PR#5707, GPR#1304): PLT stubs (used for dynamic resolution of symbols
     on Unix-like platforms) may clobber any register except those used for:
       1. C parameter passing;
       2. C return values;
       3. C callee-saved registers.
     This translates to the set { r10, r11 }.  These registers hence cannot
     be used for OCaml parameter passing and must also be marked as
     destroyed across [Ialloc] and [Ipoll] (otherwise a call to
     caml_call_gc@PLT might clobber these two registers before the assembly
     stub saves them into the GC regs block).
*)

let types_are_compatible (left : Reg.t)  (right : Reg.t) =
  match left.typ, right.typ with
  | (Int | Val | Addr), (Int | Val | Addr)
  | Float, Float
  | Float32, Float32
  | (Valx2 | Vec128), (Valx2 | Vec128) ->
    true
  | Vec256, Vec256 ->
    true
  | Vec512, Vec512 ->
    true
  | (Int | Val | Addr | Float | Float32 |
     Vec128 | Vec256 | Vec512 | Valx2), _ -> false

(* Representation of hard registers by pseudo-registers *)

let hard_int_reg =
  Regs.phys_gpr_regs
  |> Array.map (fun phys_gpr_reg ->
     Reg.create_at_location Int (Reg phys_gpr_reg))

let hard_float_reg =
  Regs.phys_simd_regs
  |> Array.map (fun phys_simd_reg ->
     Reg.create_at_location Float (Reg phys_simd_reg))

let hard_vec128_reg =
  Array.map (Reg.create_alias ~typ:Vec128) hard_float_reg
let hard_vec256_reg =
  Array.map (Reg.create_alias ~typ:Vec256) hard_float_reg
let hard_vec512_reg =
  Array.map (Reg.create_alias ~typ:Vec512) hard_float_reg
let hard_float32_reg =
  Array.map (Reg.create_alias ~typ:Float32) hard_float_reg

let add_hard_vec256_regs list ~f =
  if Arch.Extension.enabled_vec256 ()
  then f hard_vec256_reg :: list else list

let add_hard_vec512_regs list ~f =
  if Arch.Extension.enabled_vec512 ()
  then f hard_vec512_reg :: list else list

let all_phys_regs =
  [hard_int_reg; hard_float_reg; hard_float32_reg; hard_vec128_reg]
  |> add_hard_vec256_regs ~f:(fun regs -> regs)
  |> add_hard_vec512_regs ~f:(fun regs -> regs)
  |> Array.concat

let phys_reg ty (phys_reg : Regs.Phys_reg.t) =
  let index_in_class = Regs.index_in_class phys_reg in
  match (ty : machtype_component) with
  | Int | Addr | Val ->
    (* CR yusumez: We need physical registers to have the appropriate machtype
       for the LLVM backend. However, this breaks an invariant the IRC register
       allocator relies on. It is safe to guard it with this flag since the LLVM
       backend doesn't get that far. *)
    let r = hard_int_reg.(index_in_class) in
    if !Clflags.llvm_backend
    then Reg.create_alias ~typ:ty r
    else r
  | Float -> hard_float_reg.(index_in_class)
  | Float32 -> hard_float32_reg.(index_in_class)
  | Vec128 | Valx2 -> hard_vec128_reg.(index_in_class)
  | Vec256 -> hard_vec256_reg.(index_in_class)
  | Vec512 -> hard_vec512_reg.(index_in_class)

let rax = phys_reg Int (P RAX)
let rdi = phys_reg Int (P RDI)
let rdx = phys_reg Int (P RDX)
let rcx = phys_reg Int (P RCX)
let r10 = phys_reg Int (P R10)
let r11 = phys_reg Int (P R11)
let rbp = phys_reg Int (P RBP)

(* CSE needs to know that all versions of xmm15 are destroyed. *)
let destroy_xmm =
  let types =
    ([ Float; Float32; Vec128 ] : machtype_component list)
    |> add_hard_vec256_regs ~f:(fun _ : machtype_component -> Vec256)
    |> add_hard_vec512_regs ~f:(fun _ : machtype_component -> Vec512)
    |> Array.of_list
  in
  fun (mm_reg : [`SIMD] Regs.phys_reg_classed) ->
    Array.map (fun t -> phys_reg t (P mm_reg)) types

let destroyed_by_plt_stub =
  if not X86_proc.use_plt then [| |] else [| r10; r11 |]

let destroyed_by_plt_stub_set = Reg.set_of_array destroyed_by_plt_stub

let stack_slot slot ty = Reg.create_at_location ty (Stack slot)

(* Calling conventions *)

let size_domainstate_args = 64 * size_int

let calling_conventions
      ~int_registers
      ~float_registers
      ~make_stack
      ~first_stack
      arg =
  let loc = Array.make (Array.length arg) Reg.dummy in
  let int_registers = ref int_registers in
  let float_registers = ref float_registers in
  let ofs = ref first_stack in
  let max_size = ref 0 in
  (* A negative offset indicates a domainstate slot, which will
     generate unaligned moves. *)
  let align ofs size =
    if ofs <= -size then ofs
    else if ofs <= 0 then 0
    else Misc.align ofs size
  in
  for i = 0 to Array.length arg - 1 do
    let ty : machtype_component = arg.(i) in
    let registers, size =
      match ty with
      | Val | Int | Addr -> int_registers, size_int
      | Float | Float32 -> float_registers, size_float
      | Vec128 -> float_registers, size_vec128
      | Vec256 -> float_registers, size_vec256
      | Vec512 -> float_registers, size_vec512
      | Valx2 -> Misc.fatal_error "Unexpected machtype_component Valx2"
    in
    match !registers with
    | reg :: regs ->
      registers := regs;
      loc.(i) <- phys_reg ty (P reg);
      assert (not (Reg.Set.mem loc.(i) destroyed_by_plt_stub_set));
    | [] ->
      max_size := Int.max size !max_size;
      ofs := align !ofs size;
      loc.(i) <- stack_slot (make_stack !ofs) ty;
      ofs := !ofs + size;
  done;
  let pre_align, post_align =
    if !max_size >= size_vec512 then Align_64, 64
    else if !max_size >= size_vec256 then Align_32, 32
    else Align_16, 16
  in
  (loc, Misc.align (max 0 !ofs) post_align, pre_align)

let incoming ofs : Reg.stack_location =
  if ofs >= 0
  then Incoming ofs
  else Domainstate (ofs + size_domainstate_args)
let outgoing ofs : Reg.stack_location =
  if ofs >= 0
  then Outgoing ofs
  else Domainstate (ofs + size_domainstate_args)
let not_supported _ofs = fatal_error "Proc.loc_results: cannot call"

let ocaml_int_registers = Regs.[RAX; RBX; RDI; RSI; RDX; RCX; R8; R9; R12; R13]

let ocaml_float_registers =
  Regs.[MM0; MM1; MM2; MM3; MM4; MM5; MM6; MM7; MM8; MM9]

let loc_arguments arg =
  let (loc, ofs, _align) =
    calling_conventions
        ~int_registers:ocaml_int_registers
        ~float_registers:ocaml_float_registers
        ~make_stack:outgoing
        ~first_stack:(- size_domainstate_args)
        arg
  in
  loc, ofs

let loc_parameters arg =
  let (loc, _ofs, _align) =
    calling_conventions
      ~int_registers:ocaml_int_registers
      ~float_registers:ocaml_float_registers
      ~make_stack:incoming
      ~first_stack:(- size_domainstate_args)
      arg
  in
  loc

let loc_results_call res =
  let (loc, ofs, _align) =
    calling_conventions
      ~int_registers:ocaml_int_registers
      ~float_registers:ocaml_float_registers
      ~make_stack:outgoing
      ~first_stack:(- size_domainstate_args)
      res
  in
  loc, ofs

let loc_results_return res =
  let (loc, _ofs, _align) =
    calling_conventions
      ~int_registers:ocaml_int_registers
      ~float_registers:ocaml_float_registers
      ~make_stack:incoming
      ~first_stack:(- size_domainstate_args)
      res
  in loc

let max_arguments_for_tailcalls = 10 (* in regs *) + 64 (* in domain state *)

(* C calling conventions under Unix:
     first integer args in rdi, rsi, rdx, rcx, r8, r9
     first float args in xmm0 ... xmm7
     remaining args on stack
     return value in rax and rdx for integers, and xmm0 and xmm1 for floats.
  C calling conventions under Win64:
     first integer args in rcx, rdx, r8, r9
     first float args in xmm0 ... xmm3
     each integer arg consumes a float reg, and conversely
     remaining args on stack
     always 32 bytes reserved at bottom of stack.
     Return value in rax or xmm0. *)

let loc_external_results res =
  let (loc, _ofs, _align) =
    (* `~last_int:4 ~step_int:4` below is to get rdx as the second int register
       (See https://refspecs.linuxbase.org/elf/x86_64-abi-0.99.pdf, pages 21 and 22) *)
    calling_conventions
      ~int_registers:[RAX; RDX]
      ~float_registers:[MM0; MM1]
      ~make_stack:not_supported
      ~first_stack:0
      res
  in loc

let unix_loc_external_arguments arg =
  calling_conventions
    ~int_registers:[RDI; RSI; RDX; RCX; R8; R9]
    ~float_registers:[MM0; MM1; MM2; MM3; MM4; MM5; MM6; MM7]
    ~make_stack:outgoing
    ~first_stack:0
    arg

let win64_int_external_arguments = Regs.[| RCX; RDX; R8; R9 |]
let win64_float_external_arguments = Regs.[| MM0; MM1; MM2; MM3 |]

let win64_loc_external_arguments arg =
  let loc = Array.make (Array.length arg) Reg.dummy in
  let reg = ref 0
  and ofs = ref (if Config.runtime5 then 0 else 32) in
  for i = 0 to Array.length arg - 1 do
    let ty : machtype_component = arg.(i) in
    let arguments, size =
      match ty with
      | Val | Int | Addr -> win64_int_external_arguments, size_int
      | Float | Float32 -> win64_float_external_arguments, size_float
      | Vec128 | Vec256 | Vec512 ->
        (* CR mslater: (SIMD) win64 calling convention requires pass by reference *)
        Misc.fatal_error "SIMD external arguments are not supported on Win64"
      | Valx2 ->
        Misc.fatal_error "Unexpected machtype_component Valx2"
    in
    if !reg < Array.length arguments then begin
      loc.(i) <- phys_reg ty (P arguments.(!reg));
      incr reg
    end else begin
      loc.(i) <- stack_slot (Outgoing !ofs) ty;
      ofs := !ofs + size
    end
  done;
  (loc, Misc.align !ofs 16, Align_16) (* keep stack 16-aligned *)

let loc_external_arguments ty_args =
  let arg = Cmm.machtype_of_exttype_list ty_args in
  let loc, stack_ofs, align =
    if win64
    then win64_loc_external_arguments arg
    else unix_loc_external_arguments arg
  in
  Array.map (fun reg -> [|reg|]) loc, stack_ofs, align

let loc_exn_bucket = rax

let stack_ptr_dwarf_register_number = 7
let domainstate_ptr_dwarf_register_number = 14

(* Registers destroyed by operations *)

let int_regs_destroyed_at_c_call_win64 =
  if Config.runtime5
  then Regs.[|RAX; RBX; RDX; RCX; R8; R9; R10; R11; RBP|]
  else Regs.[|RAX;      RDX; RCX; R8; R9; R10; R11     |]

let int_regs_destroyed_at_c_call =
  if Config.runtime5 && not Config.no_stack_checks
  then (* Clobbers R13 to hold stack pointer. See emit.ml *)
       Regs.[|RAX; RDI; RSI; RDX; RCX; R8; R9; R13; R10; R11|]
  else Regs.[|RAX; RDI; RSI; RDX; RCX; R8; R9;      R10; R11|]

let destroyed_at_c_call_win64 =
  (* Win64: rbx, rbp, rsi, rdi, r12-r15, xmm6-xmm15 preserved *)
  [ Array.map (fun p -> phys_reg Int (P p)) int_regs_destroyed_at_c_call_win64;
    Array.sub hard_float_reg 0 6;
    Array.sub hard_float32_reg 0 6;
    Array.sub hard_vec128_reg 0 6 ]
  |> add_hard_vec256_regs ~f:(fun regs -> Array.sub regs 0 6)
  |> add_hard_vec512_regs ~f:(fun regs -> Array.sub regs 0 6)
  |> Array.concat

let destroyed_at_c_call_unix =
  (* Unix: rbx, rbp, r12-r15 preserved *)
  [ Array.map (fun p -> phys_reg Int (P p)) int_regs_destroyed_at_c_call;
    hard_float_reg;
    hard_float32_reg;
    hard_vec128_reg ]
  |> add_hard_vec256_regs ~f:(fun regs -> regs)
  |> add_hard_vec512_regs ~f:(fun regs -> regs)
  |> Array.concat

let destroyed_at_c_call =
  (* C calling conventions preserve rbx, but it is clobbered
     by the code sequence used for C calls in emit.ml, so it
     is marked as destroyed. *)
  if win64 then destroyed_at_c_call_win64 else destroyed_at_c_call_unix

let destroyed_at_alloc_or_poll =
  if X86_proc.use_plt then destroyed_by_plt_stub else [| r11 |]

let destroyed_at_pushtrap = [| r11 |]

let destroyed_at_large_memory_op =
  if Config.with_address_sanitizer then
    (* We need a scratch register [r11] to preform the address sanitizer check,
       and [rdi] might be destroyed in the event we need to call the ASAN error
       reporting function, since it's a C function accepting a single argument.
       No other registers are destroyed because the ASAN report wrappers use a
       special calling convention via the C attribute [__attribute__((preserve_all))]
       such that all registers except for [r11] are callee-saved, in order to minimize
       the amount of spilling we need to do. *)
    [| rdi; r11 |]
  else
    [||]
;;

let destroyed_at_small_memory_op =
  if Config.with_address_sanitizer then
    (* Everything stated above in the comment for [destroyed_at_large_memory_op]
       applies here too, but in addition we need one more scratch register [r10]
       in order to compute the additional [SlowPathCheck] for memory accesses that
       are smaller than one word. *)
    [| rdi; r10; r11 |]
  else
    [||]
;;

let destroyed_at_single_float64_store =
  if Config.with_address_sanitizer then
    Array.append destroyed_at_small_memory_op (destroy_xmm MM15)
  else
    (destroy_xmm MM15)
;;

let all_256bit_regs = []
  |> add_hard_vec256_regs ~f:(fun regs -> regs)
  |> add_hard_vec512_regs ~f:(fun regs -> regs)
  |> Array.concat

let all_simd_regs =
  [hard_float32_reg; hard_float_reg; hard_vec128_reg]
  |> add_hard_vec256_regs ~f:(fun regs -> regs)
  |> add_hard_vec512_regs ~f:(fun regs -> regs)
  |> Array.concat

let destroyed_by_simd_instr (instr : Simd.instr) =
  (* CR mslater: (SIMD) these don't effect regs 16-31 *)
  match[@warning "-4"] instr.id with
  | Vzeroupper -> all_256bit_regs
  | Vzeroall -> all_simd_regs
  | _ ->
    match instr.res with
    | Res_none | Arg _ -> [||]
    | Res rr ->
      Array.fold_left (fun acc ({loc; _} : Simd.arg) ->
        match Simd.loc_is_pinned loc with
        | Some RAX -> rax :: acc
        | Some RDI -> rdi :: acc
        | Some RCX -> rcx :: acc
        | Some RDX -> rdx :: acc
        | Some XMM0 ->
          let xmm0 = Array.to_list (destroy_xmm MM0) in
          xmm0 @ acc
        | None -> acc) [] rr
      |> Array.of_list

let destroyed_by_simd_op (op : Simd.operation) =
  match op.instr with
  | Instruction instr -> destroyed_by_simd_instr instr
  | Sequence seq ->
    destroyed_by_simd_instr seq.instr
    |> Array.append
      (match seq.id with
       | Sqrtss | Sqrtsd | Roundss | Roundsd
       | Pcompare_string _ | Vpcompare_string _
       | Ptestz | Ptestc | Ptestnzc
       | Vptestz_X  | Vptestc_X  | Vptestnzc_X
       | Vptestz_Y  | Vptestc_Y  | Vptestnzc_Y -> [||])

let destroyed_by_simd_mem_op (instr : Simd.Mem.operation) =
  match instr with
  | Load op | Store op -> destroyed_by_simd_op op

let destroyed_at_raise = all_phys_regs

let destroyed_at_reloadretaddr = [| |]

let destroyed_rax = [| rax |] (* CR-someday vkarvonen: Use [iarray] *)
let destroyed_rax_rdx = [| rax; rdx |]
let destroyed_rbp = [| rbp |]
let destroyed_r10 = [| r10 |]

let destroyed_at_basic (basic : Cfg_intf.S.basic) =
  match basic with
  | Reloadretaddr ->
    destroyed_at_reloadretaddr
  | Pushtrap _ ->
    destroyed_at_pushtrap
  | Op (Intop (Idiv | Imod)) | Op (Intop_imm ((Idiv | Imod), _)) ->
    destroyed_rax_rdx
  | Op(Store(Single { reg = Float64 }, _, _)) ->
    destroyed_at_single_float64_store
  | Op (Store ((Byte_unsigned | Byte_signed | Sixteen_unsigned |
                Sixteen_signed | Thirtytwo_unsigned | Thirtytwo_signed |
                Single { reg = Float32 } ), _, _))
  | Op (Load { memory_chunk =
               (Byte_unsigned | Byte_signed | Sixteen_unsigned |
                Sixteen_signed | Thirtytwo_unsigned | Thirtytwo_signed |
                Single _); _ })
  | Op(Specific (Ifloatarithmem (Float32, _, _)))
  | Op(Intop_atomic _) ->
    destroyed_at_small_memory_op
  | Op(Store( (Word_int | Word_val | Double | Onetwentyeight_aligned |
               Onetwentyeight_unaligned | Twofiftysix_aligned |
               Twofiftysix_unaligned | Fivetwelve_aligned |
               Fivetwelve_unaligned), _, _))
  | Op(Load { memory_chunk =
                (Word_int | Word_val | Double | Onetwentyeight_aligned |
                 Onetwentyeight_unaligned | Twofiftysix_aligned |
                 Twofiftysix_unaligned | Fivetwelve_aligned |
                 Fivetwelve_unaligned); _})
  | Op(Specific (Istore_int _))
  | Op(Specific (Ifloatarithmem (Float64, _, _)))
  | Op(Specific (Iprefetch _ | Icldemote _)) ->
    destroyed_at_large_memory_op
  | Op(Intop(Imulh _ | Icomp _) | Intop_imm((Icomp _), _)) ->
    destroyed_rax
  | Op (Specific (Irdtsc | Irdpmc)) ->
    destroyed_rax_rdx
  | Op Poll -> destroyed_at_alloc_or_poll
  | Op (Alloc _) ->
    destroyed_at_alloc_or_poll
  | Op (Specific Ipackf32) -> [||]
  | Op (Specific (Isimd op)) ->
    destroyed_by_simd_op op
  | Op (Specific (Isimd_mem (op,_))) ->
    destroyed_by_simd_mem_op op
  | Op (Specific (Illvm_intrinsic intr)) ->
      Misc.fatal_errorf "destroyed_at_basic: Unexpected llvm_intrinsic %s: \
                         not using LLVM backend"
        intr
  | Op (Move | Spill | Reload | Const_int _
       | Const_float _ | Const_float32 _ | Const_symbol _
       | Const_vec128 _ | Const_vec256 _ | Const_vec512 _
       | Stackoffset _
       | Intop (Iadd | Isub | Imul | Iand | Ior | Ixor | Ilsl | Ilsr
               | Iasr | Ipopcnt | Iclz | Ictz
               )
       | Int128op (Iadd128 | Isub128 | Imul64 _)
       | Intop_imm ((Iadd | Isub | Imul | Imulh _ | Iand | Ior | Ixor | Ilsl
                    | Ilsr | Iasr | Ipopcnt | Iclz | Ictz ),_)
       | Floatop _
       | Csel _
       | Reinterpret_cast _
       | Static_cast _
       | Probe_is_enabled _
       | Opaque
       | Begin_region
       | End_region
       | Specific (Ilea _ | Ioffset_loc _ | Ibswap _
                  | Isextend32 | Izextend32
                  | Ilfence | Isfence | Imfence)
       | Name_for_debugger _ | Dls_get | Tls_get | Domain_index | Hint _)
  | Poptrap _ | Prologue | Epilogue ->
    if fp then destroyed_rbp else [||]
  | Stack_check _ ->
    (* This case is used by [Cfg_available_regs].  r10 is actually saved and
       restored by the sequence to which [Stack_check] is expanded, but it may
       be clobbered in the middle. *)
    destroyed_r10

(* note: keep this function in sync with `is_destruction_point` below. *)
let destroyed_at_terminator (terminator : Cfg_intf.S.terminator) =
  match terminator with
  | Never -> assert false
  | Always _ | Parity_test _ | Truth_test _ | Float_test _ | Int_test _
  | Return | Raise _ | Tailcall_self  _ | Tailcall_func _
  | Prim {op = Probe _; _}
  ->
    if fp then [| rbp |] else [||]
  | Switch _ ->
    destroyed_rax_rdx
  | Call_no_return { func_symbol = _; alloc; ty_res = _; ty_args = _;
                     stack_ofs; stack_align = _; effects = _; }
  | Prim {op = External { func_symbol = _; alloc; ty_res = _; ty_args = _;
                          stack_ofs; stack_align = _; effects = _; }; _} ->
    assert (stack_ofs >= 0);
    if alloc || stack_ofs > 0 then all_phys_regs else destroyed_at_c_call
  | Invalid { message = _; stack_ofs; stack_align = _; label_after = _ } ->
    assert (stack_ofs >= 0);
    if stack_ofs > 0 then all_phys_regs else destroyed_at_c_call
  | Call {op = Indirect _ | Direct _; _} -> all_phys_regs

(* CR-soon xclerc for xclerc: consider having more destruction points.
   We current return `true` when `destroyed_at_terminator` returns
   `all_phys_regs` (which means we are conservative in the sense we will
   spill registers that would spill anyway); we could also return `true`
   when `destroyed_at_terminator` returns `destroyed_at_c_call` for instance. *)
(* note: keep this function in sync with `destroyed_at_terminator` above. *)
let is_destruction_point ~(more_destruction_points : bool) (terminator : Cfg_intf.S.terminator) =
  match terminator with
  | Never -> assert false
  | Always _ | Parity_test _ | Truth_test _ | Float_test _ | Int_test _
  | Return | Raise _ | Tailcall_self  _ | Tailcall_func _
  | Prim {op = Probe _; _} ->
    false
  | Switch _ ->
    false
  | Call_no_return { func_symbol = _; alloc; ty_res = _; ty_args = _;
                     stack_ofs; stack_align = _; effects = _; }
  | Prim {op = External { func_symbol = _; alloc; ty_res = _; ty_args = _;
                          stack_ofs; stack_align = _; effects = _; }; _} ->
    if more_destruction_points then
      true
    else
      if alloc || stack_ofs > 0 then true else false
  | Invalid _ -> more_destruction_points
  | Call {op = Indirect _ | Direct _; _} ->
    true

(* Layout of the stack frame *)

let initial_stack_offset ~num_stack_slots:_ ~contains_calls:_ = 0

let trap_frame_size_in_bytes = 16

let frame_required ~fun_contains_calls ~fun_num_stack_slots =
  fp || fun_contains_calls ||
  Stack_class.Tbl.exists
    fun_num_stack_slots
    ~f:(fun _stack_class num -> num > 0)

let prologue_required ~fun_contains_calls ~fun_num_stack_slots =
  frame_required ~fun_contains_calls ~fun_num_stack_slots

(* returned size includes return address *)
let frame_size ~stack_offset ~contains_calls ~num_stack_slots =
  if frame_required ~fun_contains_calls:contains_calls
     ~fun_num_stack_slots:num_stack_slots
  then begin
    let sz =
      (stack_offset
       + 8
       + Stack_class.Tbl.total_size_in_bytes num_stack_slots
       + (if fp then 8 else 0))
    in Misc.align sz 16
  end else
    stack_offset + 8

type slot_offset =
  | Bytes_relative_to_stack_pointer of int
  | Bytes_relative_to_domainstate_pointer of int

(* CR mshinwell: standardise everywhere on e.g. [contains_calls] instead
   of [fun_contains_calls] *)

let slot_offset loc ~stack_class ~stack_offset ~fun_contains_calls
      ~fun_num_stack_slots =
  match ( loc : Reg.stack_location) with
  | Incoming n ->
      Bytes_relative_to_stack_pointer (
        frame_size ~stack_offset ~contains_calls:fun_contains_calls
          ~num_stack_slots:fun_num_stack_slots
        + n)
  | Local n ->
      Bytes_relative_to_stack_pointer (
        stack_offset + Stack_class.Tbl.offset_in_bytes fun_num_stack_slots ~stack_class ~slot:n
)
  | Outgoing n -> Bytes_relative_to_stack_pointer n
  | Domainstate n ->
      Bytes_relative_to_domainstate_pointer (
        n + Domainstate.(idx_of_field Domain_extra_params) * 8)

(* Calling the assembler *)

let assemble_file infile outfile =
  X86_proc.assemble_file infile outfile

(* On amd64 the deferred-JIT-hook mechanism is implemented inside [X86_proc]
   via the [binary_content] ref written by the registered internal assembler.
   These entry points exist only so that the cross-architecture interface in
   [proc.mli] is uniform. *)
let set_pending_jit_run _ = ()

let clear_pending_jit_run () = ()

(* Precolored_regs is not always the same as [all_phys_regs], as some physical registers
   may not be allocatable (e.g. rbp when frame pointers are enabled). *)
let precolored_regs () =
  let phys_regs = Reg.set_of_array all_phys_regs in
  if fp then Reg.Set.remove rbp phys_regs else phys_regs

let has_three_operand_float_ops () = Arch.Extension.enabled AVX

let operation_supported = function
  | Cpopcnt -> Arch.Extension.enabled POPCNT
  | Creinterpret_cast (V256_of_vec (Vec128 | Vec256) | V128_of_vec Vec256)
  | Cstatic_cast (V256_of_scalar _ | Scalar_of_v256 _) ->
    Arch.Extension.enabled_vec256 ()
  | Creinterpret_cast (V512_of_vec _ | V128_of_vec Vec512 | V256_of_vec Vec512)
  | Cstatic_cast (V512_of_scalar _ | Scalar_of_v512 _) ->
    Arch.Extension.enabled_vec512 ()
  | Cprefetch _ | Catomic _
  | Capply _ | Cextcall _ | Cload _ | Calloc _ | Cstore _
  | Caddi | Csubi | Cmuli | Cmulhi _ | Cdivi | Cmodi
  | Caddi128 | Csubi128 | Cmuli64 _
  | Cand | Cor | Cxor | Clsl | Clsr | Casr
  | Ccsel _
  | Cbswap _
  | Cclz | Cctz
  | Ccmpi _ | Caddv | Cadda
  | Cnegf _ | Cabsf _ | Caddf _ | Csubf _ | Cmulf _ | Cdivf _ | Cpackf32
  | Ccmpf _
  | Craise _
  | Cprobe _ | Cprobe_is_enabled _ | Copaque | Cbeginregion | Cendregion
  | Ctuple_field _
  | Cdls_get
  | Ctls_get
  | Cdomain_index
  | Cpoll
  | Chint _
  | Creinterpret_cast (Int_of_value | Value_of_int |
                       Int64_of_float | Float_of_int64 |
                       Float32_of_float | Float_of_float32 |
                       Float32_of_int32 | Int32_of_float32 |
                       V128_of_vec Vec128)
  | Cstatic_cast (Float_of_float32 | Float32_of_float |
                  Int_of_float Float32 | Float_of_int Float32 |
                  Float_of_int Float64 | Int_of_float Float64 |
                  V128_of_scalar _ | Scalar_of_v128 _) ->
    true

let expression_supported = function
  | Cconst_int _ | Cconst_natint _ | Cconst_float32 _ | Cconst_float _
  | Cconst_vec128 _ | Cconst_symbol _  | Cvar _ | Clet _ | Cphantom_let _
  | Ctuple _ | Cop _ | Csequence _ | Cifthenelse _ | Cswitch _ | Ccatch _
  | Cexit _ | Cinvalid _ -> true
  | Cconst_vec256 _ -> Arch.Extension.enabled_vec256 ()
  | Cconst_vec512 _ -> Arch.Extension.enabled_vec512 ()

let trap_size_in_bytes () =
  if !Clflags.llvm_backend then 32 else 16
