(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*          Fabrice Le Fessant, projet Gallium, INRIA Rocquencourt        *)
(*                                                                        *)
(*   Copyright 2014 Institut National de Recherche en Informatique et     *)
(*     en Automatique.                                                    *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

[@@@ocaml.warning "+a-40-41-42"]

open! Int_replace_polymorphic_compare
open X86_ast
module DLL = Doubly_linked_list
module Section_name = X86_section.Section_name

type system =
  (* 32 bits and 64 bits *)
  | S_macosx
  | S_gnu
  | S_cygwin
  (* 32 bits only *)
  | S_solaris
  | S_win32
  | S_linux_elf
  | S_bsd_elf
  | S_beos
  | S_mingw
  (* 64 bits only *)
  | S_win64
  | S_linux
  | S_mingw64
  | S_freebsd
  | S_netbsd
  | S_openbsd
  | S_unknown

let system =
  match Config.system with
  | "macosx" -> S_macosx
  | "solaris" -> S_solaris
  | "win32" -> S_win32
  | "linux_elf" -> S_linux_elf
  | "bsd_elf" -> S_bsd_elf
  | "beos" -> S_beos
  | "gnu" -> S_gnu
  | "cygwin" -> S_cygwin
  | "mingw" -> S_mingw
  | "mingw64" -> S_mingw64
  | "win64" -> S_win64
  | "linux" -> S_linux
  | "freebsd" -> S_freebsd
  | "netbsd" -> S_netbsd
  | "openbsd" -> S_openbsd
  | _ -> S_unknown

let windows =
  match[@warning "-4"] system with
  | S_mingw64 | S_cygwin | S_win64 -> true
  | _ -> false

let is_linux = function[@warning "-4"] S_linux -> true | _ -> false

let is_macosx = function[@warning "-4"] S_macosx -> true | _ -> false

let is_win32 = function[@warning "-4"] S_win32 -> true | _ -> false

let is_win64 = function[@warning "-4"] S_win64 -> true | _ -> false

let is_solaris = function[@warning "-4"] S_solaris -> true | _ -> false

let string_of_substring_literal k n s =
  let between x low high =
    Char.compare x low >= 0 && Char.compare x high <= 0
  in
  let b = Buffer.create (n + 2) in
  let last_was_escape = ref false in
  for i = k to k + n - 1 do
    let c = s.[i] in
    if between c '0' '9'
    then
      if !last_was_escape
      then Printf.bprintf b "\\%o" (Char.code c)
      else Buffer.add_char b c
    else if
      between c ' ' '~'
      && (not (Char.equal c '"'))
      (* '"' *) && not (Char.equal c '\\')
    then (
      Buffer.add_char b c;
      last_was_escape := false)
    else (
      Printf.bprintf b "\\%o" (Char.code c);
      last_was_escape := true)
  done;
  Buffer.contents b

let string_of_string_literal s =
  string_of_substring_literal 0 (String.length s) s

let string_of_symbol prefix s =
  let spec = ref false in
  for i = 0 to String.length s - 1 do
    match String.unsafe_get s i with
    | 'A' .. 'Z' | 'a' .. 'z' | '0' .. '9' | '_' | '.' -> ()
    | _ -> spec := true
  done;
  if not !spec
  then if String.equal prefix "" then s else prefix ^ s
  else
    let b = Buffer.create (String.length s + 10) in
    Buffer.add_string b prefix;
    String.iter
      (function
        | ('A' .. 'Z' | 'a' .. 'z' | '0' .. '9' | '_' | '.') as c ->
          Buffer.add_char b c
        | c -> Printf.bprintf b "$%02x" (Char.code c))
      s;
    Buffer.contents b

let string_of_prefetch_temporal_locality_hint = function
  | Nta -> "nta"
  | T2 -> "t2"
  | T1 -> "t1"
  | T0 -> "t0"

let buf_bytes_directive b directive s =
  let pos = ref 0 in
  for i = 0 to String.length s - 1 do
    if !pos = 0
    then (
      if i > 0 then Buffer.add_char b '\n';
      Buffer.add_char b '\t';
      Buffer.add_string b directive;
      Buffer.add_char b '\t')
    else Buffer.add_char b ',';
    Printf.bprintf b "%d" (Char.code s.[i]);
    incr pos;
    if !pos >= 16 then pos := 0
  done

let string_of_reg64 = function
  | RAX -> "rax"
  | RBX -> "rbx"
  | RDI -> "rdi"
  | RSI -> "rsi"
  | RDX -> "rdx"
  | RCX -> "rcx"
  | RBP -> "rbp"
  | RSP -> "rsp"
  | R8 -> "r8"
  | R9 -> "r9"
  | R10 -> "r10"
  | R11 -> "r11"
  | R12 -> "r12"
  | R13 -> "r13"
  | R14 -> "r14"
  | R15 -> "r15"

let string_of_reg8l = function
  | RAX -> "al"
  | RBX -> "bl"
  | RCX -> "cl"
  | RDX -> "dl"
  | RSP -> "spl"
  | RBP -> "bpl"
  | RSI -> "sil"
  | RDI -> "dil"
  | R8 -> "r8b"
  | R9 -> "r9b"
  | R10 -> "r10b"
  | R11 -> "r11b"
  | R12 -> "r12b"
  | R13 -> "r13b"
  | R14 -> "r14b"
  | R15 -> "r15b"

let string_of_reg8h = function
  | AH -> "ah"
  | BH -> "bh"
  | CH -> "ch"
  | DH -> "dh"

let string_of_reg16 = function
  | RAX -> "ax"
  | RBX -> "bx"
  | RCX -> "cx"
  | RDX -> "dx"
  | RSP -> "sp"
  | RBP -> "bp"
  | RSI -> "si"
  | RDI -> "di"
  | R8 -> "r8w"
  | R9 -> "r9w"
  | R10 -> "r10w"
  | R11 -> "r11w"
  | R12 -> "r12w"
  | R13 -> "r13w"
  | R14 -> "r14w"
  | R15 -> "r15w"

let string_of_reg32 = function
  | RAX -> "eax"
  | RBX -> "ebx"
  | RCX -> "ecx"
  | RDX -> "edx"
  | RSP -> "esp"
  | RBP -> "ebp"
  | RSI -> "esi"
  | RDI -> "edi"
  | R8 -> "r8d"
  | R9 -> "r9d"
  | R10 -> "r10d"
  | R11 -> "r11d"
  | R12 -> "r12d"
  | R13 -> "r13d"
  | R14 -> "r14d"
  | R15 -> "r15d"

let string_of_regf = function
  | XMM n -> Printf.sprintf "xmm%d" n
  | YMM n -> Printf.sprintf "ymm%d" n
  | ZMM n -> Printf.sprintf "zmm%d" n

let string_of_gpr arch reg =
  match arch with X86 -> string_of_reg32 reg | X64 -> string_of_reg64 reg

let string_of_reg_idx arch reg_idx =
  match reg_idx with
  | Scalar reg -> string_of_gpr arch reg
  | Vector reg -> string_of_regf reg

let string_of_condition = function
  | E -> "e"
  | AE -> "ae"
  | A -> "a"
  | GE -> "ge"
  | G -> "g"
  | NE -> "ne"
  | B -> "b"
  | BE -> "be"
  | L -> "l"
  | LE -> "le"
  | NP -> "np"
  | P -> "p"
  | NS -> "ns"
  | S -> "s"
  | NO -> "no"
  | O -> "o"

let imm_of_float_condition = function
  | EQf -> Imm 0L
  | LTf -> Imm 1L
  | LEf -> Imm 2L
  | UNORDf -> Imm 3L
  | NEQf -> Imm 4L
  | NLTf -> Imm 5L
  | NLEf -> Imm 6L
  | ORDf -> Imm 7L

let string_of_float_condition = function
  | EQf -> "eq"
  | LTf -> "lt"
  | LEf -> "le"
  | UNORDf -> "unord"
  | NEQf -> "neq"
  | NLTf -> "nlt"
  | NLEf -> "nle"
  | ORDf -> "ord"

let float_condition_of_imm = function
  | Imm 0L -> EQf
  | Imm 1L -> LTf
  | Imm 2L -> LEf
  | Imm 3L -> UNORDf
  | Imm 4L -> NEQf
  | Imm 5L -> NLTf
  | Imm 6L -> NLEf
  | Imm 7L -> ORDf
  | Sym _ | Reg8L _ | Reg8H _ | Reg16 _ | Reg32 _ | Reg64 _ | Regf _ | Mem _
  | Mem64_RIP _ | Imm _ ->
    Misc.fatal_errorf "Invalid float condition immediate arg"

let string_of_float_condition_imm imm =
  float_condition_of_imm imm |> string_of_float_condition

let string_of_rounding = function
  | RoundDown -> "roundsd.down"
  | RoundUp -> "roundsd.up"
  | RoundTruncate -> "roundsd.trunc"
  | RoundNearest -> "roundsd.near"
  | RoundCurrent -> "roundsd"

(*= Control fields for [roundsd] operation is specified as a 4-bit immediate:
   bit 3: whether to signal Precision Floating-Point Exception.
   bit 2: if set, select rounding mode from MXCSR.RC, else use bits 0 and 1.
   bits 0 and 1: rounding mode, according to  Table 4-17 of
   Intel® 64 and IA-32 Architectures Software Developer’s Manual Volume 2. *)
let imm_of_rounding = function
  | RoundNearest -> Imm 8L
  | RoundDown -> Imm 9L
  | RoundUp -> Imm 10L
  | RoundTruncate -> Imm 11L
  | RoundCurrent -> Imm 12L

let internal_assembler = ref None

let register_internal_assembler f = internal_assembler := Some f

(* Which asm conventions to use *)
let masm =
  match[@warning "-4"] system with S_win32 | S_win64 -> true | _ -> false

let use_plt =
  match system with
  | S_macosx | S_mingw64 | S_cygwin | S_win64 -> false
  | S_linux | S_gnu | S_solaris | S_win32 | S_linux_elf | S_bsd_elf | S_beos
  | S_mingw | S_freebsd | S_netbsd | S_openbsd | S_unknown ->
    !Clflags.dlcode

(* Shall we use an external assembler command ? If [binary_content] contains
   some data, we can directly save it. Otherwise, we have to ask an external
   command. *)
let binary_content = ref None

let compile infile outfile =
  if masm
  then
    Ccomp.command
      (Config.asm ^ Filename.quote outfile ^ " " ^ Filename.quote infile
      ^ if !Clflags.verbose then "" else ">NUL")
  else
    let dwarf_flag =
      if !Clflags.native_code && !Clflags.debug
      then Dwarf_flags.get_dwarf_as_toolchain_flag ()
      else ""
    in
    Ccomp.command
      (Config.asm ^ " "
      ^ String.concat " " (Misc.debug_prefix_map_flags ())
      ^ dwarf_flag ^ " -o " ^ Filename.quote outfile ^ " "
      ^ Filename.quote infile)

let assemble_file infile outfile =
  match !binary_content with
  | None -> compile infile outfile
  | Some content ->
    content outfile;
    binary_content := None;
    0

let asm_code = DLL.make_empty ()

(* Cannot use Emitaux directly here or there would be a circular dep *)
let create_asm_file = ref true

let directive dir = DLL.add_end asm_code dir

let emit ins = directive (Ins ins)

let reset_asm_code () = DLL.clear asm_code

(* The instructions are emitted as a single flat stream, [asm_code]; the
   internal assembler consumes them grouped by section. [collect_sections] walks
   the stream and groups the lines according to the [Section] directives, which
   act as separators and do not appear in the result. *)
let collect_sections ~is_delayed =
  let sections = Section_name.Tbl.create 16 in
  let current = ref None in
  DLL.iter asm_code ~f:(fun line ->
      match[@warning "-4"] line with
      | Directive
          (Asm_targets.Asm_directives.Directive.Section
             (section, first_occurrence)) -> (
        let details =
          Asm_targets.Asm_section.details section first_occurrence
        in
        if not (Bool.equal details.is_delayed is_delayed)
        then current := None
        else
          let name =
            Section_name.make details.names details.flags details.args
          in
          match Section_name.Tbl.find_opt sections name with
          | Some instrs -> current := Some instrs
          | None ->
            let instrs = DLL.make_empty () in
            Section_name.Tbl.add sections name instrs;
            current := Some instrs)
      | dir -> !current |> Option.iter (fun instrs -> DLL.add_end instrs dir));
  Section_name.Tbl.fold
    (fun name instrs acc -> (name, instrs) :: acc)
    sections []

type output_pos = asm_line DLL.cell option (* None means the beginning *)

let current_output_pos () = DLL.last_cell asm_code

let same_output_pos (a : output_pos) (b : output_pos) =
  match a, b with
  | Some a, Some b -> DLL.same_cell a b
  | None, None -> true
  | None, Some _ | Some _, None -> false

let next_pos pos =
  match pos with None -> DLL.hd_cell asm_code | Some cell -> DLL.next cell

let output_range ~from_pos ~to_pos =
  DLL.range_to_list ~left_incl:(next_pos from_pos) ~right_excl:(next_pos to_pos)

let label_instruction_pairs ~from_pos ~to_pos ~is_first =
  let stop = next_pos to_pos in
  let is_stop cell =
    match cell, stop with
    | None, None -> true
    | Some cell, Some stop -> cell == stop
    | None, Some _ | Some _, None -> false
  in
  let new_label () =
    Asm_targets.Asm_label.create Asm_targets.Asm_section.Text
  in
  let label_line label =
    Directive (Asm_targets.Asm_directives.Directive.new_label label)
  in
  (* Track the CFA offset from the CFI directives, which mirror the emitter's
     own stack bookkeeping exactly (including the directive-only adjustment
     after path-local epilogues and the remember/restore pair around
     stack-switching C calls). At any point, the return address of the current
     frame lives at [rsp + cfa - 8]. *)
  let cfa = ref (Some 8) in
  let saved_cfa = ref [] in
  let track line =
    let module Dir = Asm_targets.Asm_directives.Directive in
    match[@warning "-4"] line with
    | Directive (Dir.Cfi_def_cfa_offset bytes) -> cfa := Some bytes
    | Directive (Dir.Cfi_adjust_cfa_offset bytes) ->
      cfa := Option.map (fun offset -> offset + bytes) !cfa
    | Directive (Dir.Cfi_def_cfa_register _) -> cfa := None
    | Directive Dir.Cfi_remember_state -> saved_cfa := !cfa :: !saved_cfa
    | Directive Dir.Cfi_restore_state -> (
      match !saved_cfa with
      | restored :: rest ->
        cfa := restored;
        saved_cfa := rest
      | [] -> cfa := None)
    | Ins _ | Directive _ -> ()
  in
  let site_retaddr_offset () =
    match !cfa with
    | None -> None
    | Some cfa ->
      let offset = cfa - 8 in
      if offset < 0 || offset land 7 <> 0
      then
        Misc.fatal_errorf
          "patchprof: return-address offset %d is negative or not a multiple \
           of 8"
          offset;
      Some offset
  in
  let rec loop cell sites =
    if is_stop cell
    then List.rev sites
    else
      match cell with
      | None -> List.rev sites
      | Some first_cell -> (
        track (DLL.value first_cell);
        let second = DLL.next first_cell in
        let found =
          match DLL.value first_cell, second with
          | Ins first, Some second_cell
            when (not (is_stop second)) && is_first first -> (
            match[@warning "-4"] DLL.value second_cell with
            | Ins (J _) -> Some second_cell
            | Ins _ | Directive _ -> None)
          | Ins _, (None | Some _) | Directive _, _ -> None
        in
        match found with
        | None -> loop second sites
        | Some second_cell ->
          let next = DLL.next second_cell in
          let first_label = new_label () in
          let second_label = new_label () in
          let end_label = new_label () in
          DLL.insert_before first_cell (label_line first_label);
          DLL.insert_before second_cell (label_line second_label);
          DLL.insert_after second_cell (label_line end_label);
          let site =
            ( first_label,
              second_label,
              end_label,
              site_retaddr_offset (),
              (Some second_cell : output_pos) )
          in
          loop next (site :: sites))
  in
  loop (next_pos from_pos) []

let peephole_optimize_from pos =
  if !Oxcaml_flags.x86_peephole_optimize
  then
    let start =
      match pos with
      | None -> DLL.hd_cell asm_code
      | Some start_excl -> DLL.next start_excl
    in
    X86_peephole_optimize.optimize_from_cell start

let generate_code asm =
  (match asm with
  | Some f -> Profile.record ~accumulate:true "write_asm" f asm_code
  | None -> ());
  match !internal_assembler with
  | Some f ->
    let instrs = collect_sections ~is_delayed:false in
    (* The delayed sections (DWARF .debug_line and .debug_frames) are emitted
       while [f] runs, after the main sections have been assembled, so they can
       only be extracted from [asm_code] once [f] forces the thunk. *)
    let delayed () = collect_sections ~is_delayed:true in
    binary_content := Some (f ~delayed instrs)
  | None -> binary_content := None
